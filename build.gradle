plugins {
  id "java"
  id "maven-publish"
  id "com.enonic.xp.base" version "3.5.2"
  id "com.github.node-gradle.node" version "7.0.2"
}

dependencies {
  compileOnly "com.enonic.xp:core-api:${xpVersion}"
  implementation "com.enonic.xp:lib-auth:${xpVersion}"
  implementation "com.enonic.xp:lib-auditlog:${xpVersion}"
  implementation "com.enonic.xp:lib-context:${xpVersion}"
  implementation "com.enonic.xp:lib-event:${xpVersion}"
  implementation "com.enonic.xp:lib-node:${xpVersion}"
  implementation "com.enonic.xp:lib-portal:${xpVersion}"
  implementation "com.enonic.xp:lib-repo:${xpVersion}"
  implementation "no.item:lib-xp-time:1.2.0"
}

repositories {
  mavenLocal()
  mavenCentral()
  xp.enonicRepo()
}

node {
  download = true
  version = "22.12.0"
}

processResources {
	exclude "**/.gitkeep"
  exclude "**/*.json"
	exclude "**/*.ts"
}

tasks.register("dev", Exec) {
  if (org.gradle.internal.os.OperatingSystem.current().isWindows()) {
    commandLine "cmd", "/c", "gradlew.bat", "deploy", "-t"
  } else {
    commandLine "./gradlew", "deploy", "-t"
  }
}

tasks.register("npmBuild", NpmTask) {
  args = [
    "run",
    "--silent",
    "build"
  ]
  dependsOn npmInstall
  environment = [
    "FORCE_COLOR": "true",
    "LOG_LEVEL_FROM_GRADLE": gradle.startParameter.logLevel.toString(),
    "NODE_ENV": project.hasProperty("dev") || project.hasProperty("development") ? "development" : "production"
  ]
  inputs.dir "src/main/resources"
  outputs.dir "build/resources/main"
  outputs.upToDateWhen { false }
}

jar.dependsOn npmBuild

tasks.register("npmCheck", NpmTask) {
	dependsOn npmInstall
	args = [
		"run",
		"check"
	]
	environment = [
		"FORCE_COLOR": "true",
	]
}

check.dependsOn npmCheck

tasks.withType(Copy).configureEach {
  includeEmptyDirs = false
}

publishing {
  repositories {
    maven {
      name = "itemtestRepository"
      url = "https://repo.itemtest.no/releases"
      credentials(PasswordCredentials)
      authentication {
        basic(BasicAuthentication)
      }
    }
  }
  publications {
    maven(MavenPublication) {
      from components.java
      groupId group
      artifactId projectName
      version version
    }
  }
}
